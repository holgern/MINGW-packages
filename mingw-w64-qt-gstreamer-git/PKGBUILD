# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Holger Nahrstaedt <holger@nahrstaedt.de>

_realname=qt-gstreamer
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r826.ea0d273
pkgrel=1
arch=('any')
pkgdesc="Qt bindings for GStreamer (mingw-w64)"
url='http://gstreamer.freedesktop.org/modules/qt-gstreamer.html'
license=('LGPL2.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-gst-plugins-good")
conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
provides=(${MINGW_PACKAGE_PREFIX}-${_realname})
source=(${_realname}::"git+https://anongit.freedesktop.org/git/gstreamer/qt-gstreamer.git")
sha256sums=('SKIP')
pkgver() {
  cd ${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  
  export QTDIR=${MINGW_PREFIX}
  
  #unset CFLAGS CXXFLAGS
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
	-DUSE_GST_PLUGIN_DIR=OFF \
	-DUSE_QT_PLUGIN_DIR=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DQTGSTREAMER_EXAMPLES=OFF \
    -DQT_VERSION=5 \
    ../${_realname}
  
  make
  
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  
  make DESTDIR="${pkgdir}" install
  cd ${pkgdir}/${MINGW_PREFIX}
  mkdir share
  cd lib
  mv qt5 ../share
}
